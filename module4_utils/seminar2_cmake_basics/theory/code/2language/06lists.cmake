#   Переменные-списки в CMake
#
#   В языке CMake существует такое понятие, как переменная, являющаяся "списком".
#   Может показаться, что это противоречит тому, что было сказано ранее, а именно тому, что
#   все переменные в CMake это строки. Но на самом деле никакого противоречия нет.
#   Дело в том, что списки в CMake тоже являются строками, где каждый элемент разделён символом ;


# Создадим список ALICE с элементами "One", "Two" и "Three"
set(ALICE "One;Two;Three")

# Напечатаем это список на экран
message("${ALICE}")


# Работа со списком происходит с помощью команды list
# Команда list, может производить множество операций со списками.
# Тип операции задаётся с помощью опции - первого аргумента команды.

# Добавим в список ALICE элемент "Four", с помощью команды list с опцией APPEND
# В результате список будет иметь вид  "One;Two;Three;Four"
list(APPEND ALICE "Four")
message("${ALICE}")


# Добавим в список ALICE ещё 2 элемента "Five" и "Six"
# В результате список будет иметь вид  "One;Two;Three;Four;Five;Six"
list(APPEND ALICE "Five;Six")
message("${ALICE}")


# Удалим из списка ALICE один элемент из конца и один элемени из начала,
# используя команду list с опциями POP_BACK и POP_FRONT соответственно.
# В результате список будет иметь вид  "Two;Three;Four;Five"
list(POP_BACK ALICE)
list(POP_FRONT ALICE)
message("${ALICE}")


# Получим элемент из списка с индексом 0 и сохраним его в переменной по имени OUTPUT.
# Для этого используем команду list с опцией GET.
# В результате в новой переменной OUTPUT запишется строка "Two".
list(GET ALICE 0 OUTPUT)
message("${OUTPUT}")


# Получим длину списка и сохраним его в переменной по имени OUTPUT.
# Для этого используем команду list с опцией LENGTH.
# В результате в новой переменной OUTPUT запишется строка "4".
list(LENGTH ALICE OUTPUT)
message("${OUTPUT}")
